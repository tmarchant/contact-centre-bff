buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.1"
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE'
    }
}

allprojects {
    buildDir = file("${rootDir}/build/${name}")
    version = "${majorVersion}.${buildNumber}"

    repositories {
        jcenter()
    }
}

task build {
    group = 'Build'
    description = 'Builds, tests and packages the application'
    dependsOn ':app:build'
}
task run(dependsOn: ':app:bootRun') {
    group = 'Application'
    description = 'Runs the app (alias for bootRun)'
}

task clean(type: Delete) {
    delete file('build')
}

repositories {
    mavenCentral()
}

ext.vertx_version = '3.3.1'

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'spring-boot'
    apply plugin: 'kotlin'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        testSupport
        testCompile.extendsFrom testSupport
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.1"
        compile 'org.codehaus.groovy:groovy-all:2.4.7'

        compile 'com.google.guava:guava:18.0'
        compile 'org.projectlombok:lombok:1.16.4'
        compile 'commons-io:commons-io:2.4'

        compile "io.vertx:vertx-unit:$vertx_version"
        compile "io.vertx:vertx-core:$vertx_version"
        compile "io.vertx:vertx-hazelcast:$vertx_version"
        compile "io.vertx:vertx-web:$vertx_version"

        testCompile group: 'junit', name: 'junit', version: '4.12'

        testSupport 'org.spockframework:spock-core:1.0-groovy-2.4', {
            exclude module: 'groovy-all'
        }
        testSupport 'com.blogspot.toomuchcoding:spock-subjects-collaborators-extension:1.1.0', {
            exclude module: 'groovy-all'
        }
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }
}

project(':app') {
    bootRepackage {
        mainClass = 'com.johnlewis.contactcentre.bff.Application'
    }

    jar {
        baseName = 'contact-centre-bff'
        destinationDir = file("${rootDir}/build/libs")
    }
}

// Configure Test projects
configure(subprojects - project(':app')) {
    configurations.compile.extendsFrom configurations.testSupport
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

apply from: 'gradle/ide.gradle'